/*
 *
 * Copyright 2019 RBKmoney
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

buildscript {
    ext {

        /* Application */
        app = [
                minSdkVersion    : 22,
                targetSdkVersion : 30,
                compileSdkVersion: 30,
        ]

        androidx = [
                appcompat       : '1.3.1',
                fragment        : '1.3.6',
                constraintlayout: '2.1.1',
                vectordrawable  : '1.1.0',
                cardview        : '1.0.0',
                recyclerview    : '1.2.1'
        ]

        decoroVersion = '1.3.5'
        playServicesWallet = '18.1.3'

        kotlinVersion = '1.5.20'
        okhttpVersion = '4.9.1'
        gsonVersion = '2.8.6'
        leakcanaryVersion = '1.6.3'
        crashliticsVersion = '2.10.1'

        datepickerVersion = '1.3.0'

        libraryVersionCode = 23
        libraryVersion = "0.0.$libraryVersionCode"
    }

    repositories {
        flatDir{dirs 'monthandyearpicker'}
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version '1.0.0.RC7'
    id "com.github.ben-manes.versions" version '0.21.0'
    id "maven-publish"
}

detekt {
    version = "1.0.0.RC7"
    profile("main") {
        input = "$projectDir/library/src/main/java"
        config = "$projectDir/gradle/linter/detekt-config.yml"
        output = "${project.buildDir}/reports/detekt"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
        parallel = true
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }

            if (rejected) {
                selection.reject('It is not release or vulnerable version of library ')
            }
        }
    }
}
