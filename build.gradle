/*
 *
 * Copyright 2019 RBKmoney
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

buildscript {
    ext {

        /* Application */
        app = [
                minSdkVersion    : 22,
                targetSdkVersion : 28,
                compileSdkVersion: 28,
        ]

        plugin = [
                dexcount : '0.8.6',
                fabric   : '1.29.0'
        ]

        androidx = [
                appcompat       : '1.0.2',
                fragment        : '1.0.0',
                constraintlayout: '1.1.3',
                annotation      : '1.0.2',
                vectordrawable  : '1.0.1',
                cardview        : '1.0.0',
                recyclerview    : '1.0.0'
        ]

        decoroVersion = '1.3.5'
        playServicesWallet = '17.0.0'

        kotlinVersion = '1.3.31'
        okhttpVersion = '3.14.2'
        gsonVersion = '2.8.5'
        leakcanaryVersion = '1.6.3'
        crashliticsVersion = '2.10.1'

        datepickerVersion = '1.3.0'
    }

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:$plugin.dexcount"
        classpath "io.fabric.tools:gradle:$plugin.fabric"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version '1.0.0.RC7'
    id "com.github.ben-manes.versions" version '0.21.0'
}

detekt {
    version = "1.0.0.RC7"
    profile("main") {
        input = "$projectDir/app/src/main/java"
        config = "$projectDir/gradle/linter/detekt-config.yml"
        output = "${project.buildDir}/reports/detekt"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
        parallel = true
    }
}


allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }

            if (rejected) {
                selection.reject('It is not release or vulnerable version of library ')
            }
        }
    }
}
