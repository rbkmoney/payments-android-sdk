/*
 *
 * Copyright 2019 RBKmoney
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply plugin: 'com.novoda.bintray-release'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.jfrog.bintray'

dexcount {
    format = "tree"
    includeClasses = true
    includeClassCount = true
    includeFieldCount = true
    includeTotalMethodCount = true
    orderByMethodCount = true
    verbose = true
    maxTreeDepth = Integer.MAX_VALUE
    teamCityIntegration = false
    teamCitySlug = null
    runOnEachPackage = true
    maxMethodCount = Integer.MAX_VALUE
    enabled = true
}

android {
    compileSdkVersion app.compileSdkVersion

    defaultConfig {
        minSdkVersion app.minSdkVersion
        targetSdkVersion app.targetSdkVersion
        versionCode libraryVersionCode
        versionName libraryVersion
        buildConfigField("String", "VERSION_NAME", "\"${versionName}\"")
    }

    resourcePrefix 'rbk_'

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations {
    optional
    implementation.extendsFrom optional
}

def playServicesAdsVersion = '18.1.0'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    compileOnly "com.google.android.gms:play-services-ads:$playServicesAdsVersion"

    /* androidx */
    implementation "androidx.appcompat:appcompat:$androidx.appcompat"
    implementation "androidx.fragment:fragment:$androidx.fragment"
    implementation "androidx.constraintlayout:constraintlayout:$androidx.constraintlayout"
    implementation "androidx.vectordrawable:vectordrawable:$androidx.vectordrawable"
    implementation "androidx.cardview:cardview:$androidx.cardview"
    implementation "androidx.recyclerview:recyclerview:$androidx.recyclerview"

    /* play services */
    implementation "com.google.android.gms:play-services-wallet:$playServicesWallet"

    /* serialization */
    implementation "com.google.code.gson:gson:$gsonVersion"

    /* OkHttp */
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

    implementation "com.whiteelephant:monthandyearpicker:$datepickerVersion"
    implementation "ru.tinkoff.decoro:decoro:$decoroVersion"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

archivesBaseName = "payments-android-sdk"

publish {

    def groupProjectID = 'rbk.money'
    def artifactProjectID = 'payments-android-sdk'
    def publishVersionID = libraryVersion

    userOrg = 'rbkmoney-org'
    groupId = groupProjectID

    repoName = artifactProjectID
    artifactId = artifactProjectID

    publishVersion = publishVersionID

    desc = 'RBK.money Payment Android SDK'
    website = 'https://rbk.money/'
    repository = 'https://github.com/rbkmoney/payments-android-sdk'
    version libraryVersion

    publishing {
        publications {
            Project(MavenPublication) {
                groupId = groupProjectID
                artifactId = artifactProjectID
                version = libraryVersion

                artifact("$buildDir/outputs/aar/payments-android-sdk-release.aar")
                artifact sourcesJar

                pom.withXml {
                    def root = asNode()

                    // Basic Info
                    root.appendNode('name', 'RBK.money Payment Android SDK')
                    root.appendNode('description', 'The Android SDK for RBK.money')
                    root.appendNode('url', 'https://rbk.money/')

                    // License Info
                    def license = root.appendNode('licenses').appendNode('license')
                    license.appendNode('name', 'The Apache Software License, Version 2.0')
                    license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0')
                    license.appendNode('distribution', 'repo')

                    // Source Control Info
                    def scm = root.appendNode('scm')
                    scm.appendNode('url', 'https://github.com/rbkmoney/payments-android-sdk')
                    scm.appendNode('connection', 'git@github.com:rbkmoney/payments-android-sdk.git')

                    // Dependencies
                    def dependenciesNode = root.appendNode('dependencies')

                    configurations.implementation.allDependencies.each { dp ->
                        if (dp.group) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', dp.group)
                            dependencyNode.appendNode('artifactId', dp.name)
                            dependencyNode.appendNode('version', dp.version)
                        }
                    }

                    // Developer info
                    def developers = root.appendNode('developers')
                    def dev = developers.appendNode('developer')
                    dev.appendNode('id', 'rbkmoney')
                    dev.appendNode('name', 'rbkmoney')
                    dev.appendNode('email', 'developers@rbk.money')
                }
            }
        }
    }
    publications = ['Project']
}
