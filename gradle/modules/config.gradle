/*
 *
 * Copyright 2019 RBKmoney
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import java.text.NumberFormat
import java.text.ParsePosition

class ConfigPlugin implements Plugin<Project> {

    private
    def timeSpecClassName = "com.ertelecom.domru.router.data.service.settings.entity.TimeSpec"

    private def timeUnitMinutes = "java.util.concurrent.TimeUnit.MINUTES"
    private def timeUnitSeconds = "java.util.concurrent.TimeUnit.SECONDS"

    private def configDefault = "default.properties"
    private def configFolder = "config"

    void apply(Project project) {
        def propsFolder = new File("${project.rootDir}/$configFolder")

        propsFolder.listFiles(new FilenameFilter() {
            @Override
            boolean accept(File file, String s) {
                return s != configDefault
            }
        })
                .each { writeProperties(project, it) }

        writeProperties(project, new File("${project.rootDir}/$configFolder/$configDefault"))
    }

    def writeProperties(Project project, File propFile) {
        Properties props = new Properties()
        props.load(new FileInputStream(propFile))
        project.android.buildTypes.each {
            TreeMap fields = it.getBuildConfigFields()

            props.forEach { key, value ->
                if (!fields.containsKey(key)) {
                    if (isInteger(value)) {
                        it.buildConfigField("int", key, value)
                    } else if (isLong(value)) {
                        it.buildConfigField("long", key, value)
                    } else if (isTimeMins(value)) {
                        it.buildConfigField(timeSpecClassName, key,
                                "new $timeSpecClassName(${cropLast(value)}, $timeUnitMinutes)")
                    } else if (isTimeSeconds(value)) {
                        it.buildConfigField(timeSpecClassName, key,
                                "new $timeSpecClassName(${cropLast(value)}, $timeUnitSeconds)")
                    } else {
                        it.buildConfigField("String", key, value)
                    }
                }
            }
        }
    }

    def isInteger = {
        def formatter = NumberFormat.instance
        def pos = [0] as ParsePosition
        formatter.parse(it, pos)
        pos.index == it.size()
    }

    def isLong = { isNumberWithEnd(it, "L") || isNumberWithEnd(it, "l") }

    def isTimeMins = { isNumberWithEnd(it, "m") }

    def isTimeSeconds = { isNumberWithEnd(it, "s") }

    def cropLast = {
        it.substring(0, it.size() - 1)
    }

    static boolean isNumberWithEnd(String value, String end) {
        def formatter = NumberFormat.instance
        def pos = [0] as ParsePosition
        formatter.parse(value, pos)
        return (pos.index == value.size() - 1) && (value.endsWith(end))
    }
}

apply plugin: ConfigPlugin